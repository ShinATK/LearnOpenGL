#cg #opengl 

---
## [阴影映射](https://learnopengl-cn.github.io/05%20Advanced%20Lighting/03%20Shadows/01%20Shadow%20Mapping/)

**Shadow Mapping**

**阴影**：一个光源的光线由于其他物体的阻挡不能够达到一个物体的表面的时候
- 使场景更真实
- 反应物体之间的空间位置关系
- 提升场景和物体的深度感

*阴影不容易实现，当前实时渲染领域还没有找到一种完美的阴影算法*

## 阴影贴图：实现阴影的技术其中之一

使用较多的一种技术：**阴影贴图 shadow mapping**
- 相对容易实现
- 性能不会太低
- 容易扩展为高级算法
	- Omnidirectional Shadow Maps
	- Cascaded Shadow Maps

**阴影映射的思路**：*从光的位置出发看向场景*，能看到的东西都将被点亮，看不到的地方则会处于阴影之中

具体方法：
- 从光源投射光线，记录射线第一次击中的片段位置
- 从该击中位置继续延长，检查场景中是否有其他点被击中
- 如果该点深度值比最近点更大，则该点在阴影中

如图示意：![](img/Pasted%20image%2020240107144040.png)

*但计算从光源出发的大量射线和场景的交点是非常消耗资源的*

另一种方法：借助**深度缓冲**，主要思路是：
- 观察者视角看到某个点
- 光源到该点连线方向，在阴影贴图中寻找该方向上的深度值
- 阴影贴图中的深度值比该点深度值小，说明该点在阴影中

如图示意：![](img/Pasted%20image%2020240107144103.png)

具体的算法步骤：
- 从光源方向渲染深度贴图
- 再从正常的观察者方向渲染场景，并根据深度贴图判断片段是否处于阴影中

## 深度贴图：阴影贴图的实现

**Depth Map**

“*从光的透视图中渲染的深度纹理，用来计算阴影*”

具体步骤如下：

- 为深度贴图创建帧缓冲对象
```cpp
GLuint depthMapFBO;
glGenFramebuffers(1, &depthMapFBO);
```

- 创建 2D 纹理，并设置纹理的宽高（深度贴图的分辨率）
```cpp
const GLuint SHADOW_WIDTH = 1024, SHADOW_HEIGHT = 1024; 
GLuint depthMap; 

// 生成纹理单元
glGenTextures(1, &depthMap); 

// 绑定纹理单元
glBindTexture(GL_TEXTURE_2D, depthMap); 

// 生成目标纹理
glTexImage2D(GL_TEXTURE_2D, 0, GL_DEPTH_COMPONENT, SHADOW_WIDTH, SHADOW_HEIGHT, 0, GL_DEPTH_COMPONENT, GL_FLOAT, NULL); 

// 设置纹理过滤和环绕方式
glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST); 
glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_NEAREST); 
glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_REPEAT); 
glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_REPEAT);
```

- 将深度纹理作为帧缓冲的深度缓冲，因为不需要颜色缓冲，需要显式告诉 OpenGL 渲染时不使用颜色数据
```cpp
glBindFramebuffer(GL_FRAMEBUFFER, depthMapFBO);
glFramebufferTexture2D(GL_FRAMEBUFFER, GL_DEPTH_ATTACHMENT, GL_TEXTURE_2D, depthMap, 0);

// 显式设置不读取颜色数据
glDrawBuffer(GL_NONE);
glReadBuffer(GL_NONE);
glBindFramebuffer(GL_FRAMEBUFFER, 0);
```

- 生成深度纹理贴图
```cpp
// 渲染深度贴图, 注意需要将视口大小设置贴图分辨率
glViewport(0, 0, SHADOW_WIDTH, SHADOW_HEIGHT);
glBindFramebuffer(GL_FRAMEBUFFER, depthMapFBO);
glClear(GL_DEPTH_BUFFER_BIT);

ConfigureShaderAndMatrices();

RenderScene();
glBindFramebuffer(GL_FRAMEBUFFER, 0);
```

- 给视口大小四边形设置该深度贴图，用以 debug 
```cpp
// 使用深度贴图渲染场景，注意记得将视口大小调回
glViewport(0, 0, SCR_WIDTH, SCR_HEIGHT);
glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT);

ConfigureShaderAndMatrices();

glBindTexture(GL_TEXTURE_2D, depthMap);
RenderScene();
```

## 光源空间的变换

因为需要从光源出发计算场景的深度，所以需要针对光源空间设置相机空间 `view` 和投影空间 `projection`

对于相机空间，`view` 矩阵：
- 光源位置：`glm::vec(-2.0f, 4.0f, -1.0f)`
- 光源视角方向：`glm::vec3(0.0f, 0.0f, 0.0f)`
- 光源上方：`glm::vec3(0.0f, 1.0f, 0.0f)`

```cpp
glm::mat4 lightView = glm::lookAt(glm::vec(-2.0f, 4.0f, -1.0f), glm::vec3(0.0f, 0.0f, 0.0f), glm::vec3(0.0f, 1.0f, 0.0f));
```

对于投影空间
- 透视投影
- 正交投影

因为此处使用的是平行光，为防止场景的透视图出现变形从而导致阴影发生改变，所以这里使用*正交投影*

```cpp
GLfloat near_plane = 1.0f, far_plane = 7.5f;
glm::mat4 lightProjection = glm::ortho(-10.0f, 10.0f, -10.0f, 10.0f, near_plane, far_plane);

```

二者共同构成光空间的变换矩阵

## 渲染至深度贴图

至此，我们生成了存储场景深度值的深度贴图，给出了光空间变换矩阵，接下来需要设置对应的着色器

先来明确一下两个着色器的作用，

对于顶点着色器，这里的目的是将物体的世界坐标变换到光空间坐标，故：
```c
#version 450 core
layout (location=0) in vec3 position;

uniform mat4 lightSpaceMatrix;
uniform mat4 model;

void main()
{
	gl_Position = lightSpaceMatrix * model * vec4(position, 1.0f);
}
```

对于片段着色器，因为这里只考虑片段的深度值，不需要计算颜色值，故片段着色器实际上不需要进行任何操作
```c
#version 450 core
void main()
{
	// gl_FragDepth = gl_FragCoord.z;
}
```

注：注释内容实际上不需要运行，因为着色器回默认去设置深度缓冲

所以，上方代码生成深度贴图处的 `ConfigureShaderAndMatrices();` 内容可以代码描述为：
```cpp
simpleDepthShader.use();
glUniformMatrix4fv(lightSpaceMatrixLocation, 1, GL_FALSE, glm::value_ptr(lightSpaceMatrix));

glViewport(0, 0, SHADOW_WIDTH, SHADOW_HEIGHT);
glBindFramebuffer(GL_FRAMEBUFFER, depthMapFBO);
glClear(GL_DEPTH_BUFFER_BIT);
RenderScene(simpleDepthShader);
glBindFramebuffer(GL_FRAMEBUFFER, 0);
```

为了方便调试 debug，这里可以渲染到一个视口大小的四边形上，利用标准化设备空间设置四个顶点，并设置深度贴图，其片段着色器为：
```c
#version 450 core
out vec4 color;
in vec2 TexCoords;

uniform sampler2D depthMap;
void main()
{
	float depthValue = texture(depthMap, TexCoords).r;
	color = vec4(vec3(depthValue), 1.0);
}
```

## 渲染阴影

按照阴影贴图生成阴影的思路，目前已经通过光空间获得了场景中会产生阴影的位置信息

接下来需要将观察者视野内出现的片段坐标，先计算其在光空间中对应位置信息，再对比深度贴图中的深度值，来确定该片段是否处于阴影中

即，渲染场景的片段着色器需要额外提供*比较片段在光空间中深度值和深度贴图中的深度信息的大小的功能*

- 顶点着色器的功能只需要额外获取一个光空间变换矩阵
```c
#version 450 core
layout (location = 0) in vec3 position;
layout (location = 1) in vec3 normal;
layout (location = 2) in vec2 texCoords;

out vec2 TexCoords;

out VS_OUT{
	vec3 FragPos;
	vec3 Normal;
	vec2 TexCoords;
	vec4 FragPosLightSpace;
} vs_out;

uniform mat4 projection;
uniform mat4 view;
uniform mat4 model;
uniform mat4 lightSpaceMatrix;

void main()
{
	gl_Position = projection * view * model * vec4(position, 1.0f);
	vs_out.FragPos = vec3(model * vec4(position, 1.0));
	vs_out.Normal = transpose(inverse(mat3(model))) * normal;
	vs_out.TexCoords = texCoords;
	vs_out.FragPosLightSpace = lightSpaceMatrix * vec4(vs_out.FragPos, 1.0);
}
```

- 片段着色器
```c
#version 450 core
out vec4 FragColor;

in VS_OUT {
	vec3 FragPos;
	vec3 Normal;
	vec2 TexCoords;
	vec4 FragPosLightSpace;
} fs_in;

uniform sampler2D diffuseTexture;
uniform sampler2D shadowMap;
uniform vec3 lightPos;
uniform vec3 viewPos;

// 确认片段是否在阴影中
float ShadowCalculation(vec4 fragPosLightSpace) { [...] } 

void main() {
	vec3 color = texture(diffuseTexture, fs_in.TexCoords).rgb;
	vec3 normal = normalize(fs_in.Normal); vec3 lightColor = vec3(1.0); 
	// Ambient
	vec3 ambient = 0.15 * color;
	// Diffuse
	vec3 lightDir = normalize(lightPos - fs_in.FragPos); float diff = max(dot(lightDir, normal), 0.0); vec3 diffuse = diff * lightColor;
	// Specular
	vec3 viewDir = normalize(viewPos - fs_in.FragPos);
	vec3 reflectDir = reflect(-lightDir, normal);
	float spec = 0.0;
	vec3 halfwayDir = normalize(lightDir + viewDir);
	spec = pow(max(dot(normal, halfwayDir), 0.0), 64.0);
	vec3 specular = spec * lightColor;
	
	// 计算阴影
	float shadow = ShadowCalculation(fs_in.FragPosLightSpace);
	vec3 lighting = (ambient + (1.0 - shadow) * (diffuse + specular)) * color;
	
	FragColor = vec4(lighting, 1.0f);
}
```

- *注意*
- 顶点着色器中输出到 `gl_Position` 时，会自动进行透视除法，将空间坐标范围转换为 `[-1, 1]`
- 而这里在 `ShadowCalculation()` 中计算片段在光空间坐标时，需要手动进行透视除法

下面是 `ShadowCalculation()` 的代码：
```c
float ShadowCalculation(vec4 fragPosLightSpace) {
	// 执行透视除法
	vec3 projCoords = fragPosLightSpace.xyz / fragPosLightSpace.w;
	
	// 变换到[0,1]的范围
	projCoords = projCoords * 0.5 + 0.5;
	
	// 取得最近点的深度(使用[0,1]范围下的fragPosLight当坐标)
	float closestDepth = texture(shadowMap, projCoords.xy).r;
	
	// 取得当前片段在光源视角下的深度
	float currentDepth = projCoords.z;
	
	// 检查当前片段是否在阴影中
	float shadow = currentDepth > closestDepth ? 1.0 : 0.0;
	
	return shadow;
}
```

最终效果：![](img/Pasted%20image%2020240107205816.png)

## 阴影贴图的改进

可以发现图中出现了奇怪的线条纹路，这种不真实感叫做**阴影失真（Shadow Acne）**

这种现象出现的原因：
- 深度贴图的分辨率
- 法线和光线的夹角大小

参考链接：
- [关于 Shadow Mapping 产生的 Shadow Acne，我的理解是不是有问题？ - 王十一的回答 - 知乎](https://www.zhihu.com/question/49090321/answer/114217482)
- [制作 shadow map 时，为什么剔除正面可以解决物体悬浮问题（Peter panning 问题）？ - 核桃的回答 - 知乎](https://www.zhihu.com/question/321779117/answer/1948055358)

简单来说，因为深度贴图分辨率问题，在根据片段的 xy 计算对应纹理位置时，容易出现相邻的几个片段 xy 计算后得到的纹理位置为同一个

而这里需要的是深度信息，再加上光线会以一定倾斜角度入射片段这就导致出现如下图的情况，

![](img/Pasted%20image%2020240107210928.png)

看图中，abcd 四个点共用一个纹理坐标信息，其对应的深度值为 10，而经过片段着色器计算，ad 由于更靠近光源垂直位置变换到光空间后深度值必然小于 10，而 cb 的深度值会大于 10

这就导致了四个点本都应该被照亮的情况下，仍然出现一半的位置被判定为处于阴影中


## 阴影偏移

shadow bias

为解决上边出现的阴影失真问题，可以通过在和深度贴图的深度信息进行对比时给片段的光空间深度信息设置一个偏差值 bias
根据我们刚刚的分析，cb 处于阴影中的原因是因为其在光空间中的深度信息大于深度贴图在该位置的深度信息

所以，我们只需要设置一个合适的 bias 值，让 cb 在光空间的深度值小于 10 即可

下面为 shadow bias 设置为 0.005 时的结果：![](img/Pasted%20image%2020240107211805.png)

## 悬浮

但引入阴影偏移后又会导致**悬浮 peter panning**的问题，即物体和阴影之间的错位导致视觉上看起来物体像是漂浮在空中一般，如下图

![](img/Pasted%20image%2020240107211907.png)

出现该现象的原因，因为我们设置了一个 shadow bias，这其实导致了用来判定阴影的平面比我们在空间中设置的地板平面更高，如下图![](img/Pasted%20image%2020240107212147.png)

- 因为我们整体缩短了片段在光空间中到光源位置的距离
- 相当于是我们用一个更远位置的阴影贴图去判断了整体上更靠近光源的一个平面上的阴影产生问题

解决该问题的一个技巧是，利用**正面剔除 front face culling**

即，开启正面 cullFace，但实际上这种操作其实并没有真正解决，或者说这种操作本质上是解决的**Shadow Acne**，而不是悬浮问题

剔除正面后只是将出现 shadow acne 的位置从正面调整到了背面而已，如果此时将摄像机移动到正方体内部，可以看到接触地面的位置出现了 shadow acne，只是从外部看不到，从而产生了解决了的假象，如下图![](img/Pasted%20image%2020240107213155.png)

并且利用正面剔除的方法会导致物体接触位置出现漏光，这就是因为其内部出现 shadow acne 而导致的：![](img/Pasted%20image%2020240107213231.png)

如果继续使用 bias 方法，peter panning 现象又会出现

值得一提的是，这种正面剔除方法只适用于有体积的物体，像地板这种只有一个面的，会导致阴影消失


## 采样过多

当我们拉远视角，法线在场景出现了明显的亮暗分界

![](img/Pasted%20image%2020240107213422.png)

这是因为这部分区域*超出了光的视锥*，即位于光的视锥的不可见区域，这些区域会被默认为处于阴影中，而这个光照可见区域气势上代表了深度贴图的大小


## PCF

当我们仔细观察阴影边缘，会发现这些位置出现了很明显的锯齿

这一现象的原因同样是因为深度贴图的分辨率是固定的，导致多个片段对应一个纹理像素

可以借鉴 MSAA 的思想，在这里叫做**PCF Percentage-closer filtering**

```c
float shadow = 0.0;
vec2 texelSize = 1.0 / textureSize(shadowMap, 0);
for(int x = -1; x <= 1; ++x)
{
	for(int y = -1; y <= 1; ++y)
	{
		float pcfDepth = texture(shadowMap, projCoords.xy + vec2(x, y) * texelSize).r;
		shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;
	}
}
shadow /= 9.0;
```

## 正交 vs 投影

在计算平行光的光空间变换矩阵时，使用的是正交投影矩阵

正交投影和透视投影矩阵的阴影效果可参考下图：![](img/Pasted%20image%2020240107213920.png)

- 正交投影矩阵：不会对场景进行透视变形，即*所有视图/光线都是平行的*，和定向光产生阴影的原理是一致的
- 透视投影：所有顶点根据透视关系进行变形，更适用于点光源和聚光灯

**另一个差别**
- 透视投影矩阵在将深度缓冲视觉化后经常会得到几乎全白的结果
- 原因：透视投影下深度为非线性，导致大部分可辨识范围都位于近平面附近（可以参考 Gamma 矫正？）
- 所以需要额外进行一次非线性转线性的变化

```c
#version 330 core
out vec4 color;
in vec2 TexCoords;

uniform sampler2D depthMap;
uniform float near_plane;
uniform float far_plane;

float LinearizeDepth(float depth)
{
	float z = depth * 2.0 - 1.0; // Back to NDC
	return (2.0 * near_plane * far_plane) / (far_plane + near_plane - z * (far_plane - near_plane));
}

void main()
{
	float depthValue = texture(depthMap, TexCoords).r;
	color = vec4(vec3(LinearizeDepth(depthValue)/fra_plane), 1.0); // persepective
	// color - vec4(vec3(depthValue), 1.0);
}
```




